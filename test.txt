-- -----------------------------------------------------------------------------
-- Script name:   AVSA_VAR_VOD_30D.hql
-- Created on:    12/11/2015
-- Authors:       Stefanelli, Adriano - Fumagalli, Luca - Melchiorre, Pierpaolo
-- Purpose:       Process to calculate the VAR Vod 30D KPIs
-- -----------------------------------------------------------------------------

-- creating a temporary table to store the 1^ subset of VOD's KPIs
CREATE EXTERNAL TABLE IF NOT EXISTS var_vod_30D_tmp01 (     
  content_id STRING,
  title STRING,
  genre STRING,
  category STRING,
  duration_min STRING,
  attribute_1 STRING,
  attribute_2 STRING,
  attribute_3 STRING,
  attribute_4 STRING,
  attribute_5 STRING,
  attribute_6 STRING,
  attribute_7 STRING,
  attribute_8 STRING,
  attribute_9 STRING,
  attribute_10 STRING,
  attribute_11 STRING,
  attribute_12 STRING,
  attribute_13 STRING,
  attribute_14 STRING,
  attribute_15 STRING,
  attribute_16 STRING,
  attribute_17 STRING,
  attribute_18 STRING,
  attribute_19 STRING,
  attribute_20 STRING,
  logged_viewers_30D INT,
  anonymous_viewers_30D INT,
  logged_views_30D INT,
  anonymous_views_30D INT,
  playback_duration_min_per_logged_viewer_30D INT,
  playback_duration_min_per_anonymous_viewer_30D INT,
  playback_duration_min_per_logged_view_30D INT,
  playback_duration_min_per_anonymous_view_30D INT
)
ROW FORMAT
DELIMITED FIELDS TERMINATED BY '\073'
LINES TERMINATED BY '\n'
STORED AS TEXTFILE  
LOCATION '${hivevar:root_path}/agg/VAR_VOD/var_vod_30D_tmp01';      

-- -----------------------------------------------------------------------------
-- Temporary table name: var_vod_30D_tmp01
-- LOGGED_VIEWERS_30D
-- ANONYMOUS_VIEWERS_30D
-- LOGGED_VIEWS_30D
-- ANONYMOUS_VIEWS_30D
-- PLAYBACK_DURATION_MIN_PER_LOGGED_VIEWER_30D
-- PLAYBACK_DURATION_MIN_PER_ANONYMOUS_VIEWER_30D
-- PLAYBACK_DURATION_MIN_PER_LOGGED_VIEW_30D
-- PLAYBACK_DURATION_MIN_PER_ANONYMOUS_VIEW_30D
-- -----------------------------------------------------------------------------
INSERT OVERWRITE TABLE var_vod_30D_tmp01
SELECT 
  vdm.content_id,
  vdm.title,
  vdm.genre,
  vdm.category,
  vdm.duration_min,
  vdm.attribute_1,
  vdm.attribute_2,
  vdm.attribute_3,
  vdm.attribute_4,
  vdm.attribute_5,
  vdm.attribute_6,
  vdm.attribute_7,
  vdm.attribute_8,
  vdm.attribute_9,
  vdm.attribute_10,
  vdm.attribute_11,
  vdm.attribute_12,
  vdm.attribute_13,
  vdm.attribute_14,
  vdm.attribute_15,
  vdm.attribute_16,
  vdm.attribute_17,
  vdm.attribute_18,
  vdm.attribute_19,
  vdm.attribute_20,
  b.logged_viewers_30D,
  b.anonymous_viewers_30D,
  b.logged_views_30D,
  b.anonymous_views_30D,
  b.playback_duration_min_per_logged_viewer_30D,
  b.playback_duration_min_per_anonymous_viewer_30D,
  b.playback_duration_min_per_logged_view_30D,
  b.playback_duration_min_per_anonymous_view_30D
FROM var_vod_master vdm 
LEFT OUTER JOIN (
  SELECT
    t4.content_id                                                       AS CONTENTID,
    count(distinct LOGGED_VIEWERS_30D)                                   AS LOGGED_VIEWERS_30D,
    count(distinct ANONYMOUS_VIEWERS_30D)                                AS ANONYMOUS_VIEWERS_30D,
    sum(LOGGED_VIEWS_30D)                                                AS LOGGED_VIEWS_30D,
    sum(ANONYMOUS_VIEWS_30D)                                             AS ANONYMOUS_VIEWS_30D, 
    sum(t2.CONSUMPTION_LOGGED) / count(distinct LOGGED_VIEWERS_30D)      AS PLAYBACK_DURATION_MIN_PER_LOGGED_VIEWER_30D,
    sum(t2.CONSUMPTION_ANONYMUS) / count(distinct ANONYMOUS_VIEWERS_30D) AS PLAYBACK_DURATION_MIN_PER_ANONYMOUS_VIEWER_30D,
    sum(t2.CONSUMPTION_LOGGED) / sum(LOGGED_VIEWS_30D)                   AS PLAYBACK_DURATION_MIN_PER_LOGGED_VIEW_30D,
    sum(t2.CONSUMPTION_ANONYMUS) / sum(ANONYMOUS_VIEWS_30D)              AS PLAYBACK_DURATION_MIN_PER_ANONYMOUS_VIEW_30D
  FROM var_vod_master t4
  LEFT OUTER JOIN (
    SELECT
      t3.contentid                                                     AS CONTENTID,
      t0.userid                                                        AS USERID,
      t0.deviceid                                                      AS DEVICEID,
      (if(t0.userid != t0.deviceid, round(t0.consumption /60), null))  AS CONSUMPTION_LOGGED,
      (if(t0.userid = t0.deviceid, round( t0.consumption /60), null))  AS CONSUMPTION_ANONYMUS,
      (if(t0.userid != t0.deviceid, t0.userid, null))                  AS LOGGED_VIEWERS_30D,
      (if(t0.userid = t0.deviceid, t0.deviceid, null))                 AS ANONYMOUS_VIEWERS_30D,
      (if(t0.userid != t0.deviceid,1, 0))                              AS LOGGED_VIEWS_30D,
      (if(t0.userid = t0.deviceid, 1, 0))                              AS ANONYMOUS_VIEWS_30D
    FROM target_watching t0 
    JOIN target_category_map t3 ON ( t0.contentid = t3.contentid ) 
   WHERE t0.dt_reference >= DATE_SUB(FROM_UNIXTIME(UNIX_TIMESTAMP(),'yyyy-MM-dd') , 30) 
                    AND t0.dt_reference < FROM_UNIXTIME(UNIX_TIMESTAMP(),'yyyy-MM-dd') 
     AND t0.download != "Y"
    ) t2 ON ( t4.content_id = t2.contentid ) 
 GROUP BY 
   t4.content_id
) b ON ( vdm.content_id = b.contentid );

-- creating a temporary table to store the 2^ subset of VAR Vod's KPIs
CREATE EXTERNAL TABLE IF NOT EXISTS var_vod_30D_tmp02 (
  content_id STRING,
  title STRING,
  genre STRING,
  category STRING,
  duration_min DOUBLE,
  attribute_1 STRING,
  attribute_2 STRING,
  attribute_3 STRING,
  attribute_4 STRING,
  attribute_5 STRING,
  attribute_6 STRING,
  attribute_7 STRING,
  attribute_8 STRING,
  attribute_9 STRING,
  attribute_10 STRING,
  attribute_11 STRING,
  attribute_12 STRING,
  attribute_13 STRING,
  attribute_14 STRING,
  attribute_15 STRING,
  attribute_16 STRING,
  attribute_17 STRING,
  attribute_18 STRING,
  attribute_19 STRING,
  attribute_20 STRING,
  views_subsequent_details_30D DOUBLE
)
ROW FORMAT
DELIMITED FIELDS TERMINATED BY '\073'
LINES TERMINATED BY '\n'
STORED AS TEXTFILE
LOCATION '${hivevar:root_path}/agg/VAR_VOD/var_vod_30D_tmp02';

-- -----------------------------------------------------------------------------
-- Temporary table name: var_vod_30D_tmp02
-- VIEWS_SUBSEQUENT_DETAILS_30D
-- -----------------------------------------------------------------------------

INSERT OVERWRITE TABLE var_vod_30D_tmp02
SELECT 
  vdm.content_id,
  vdm.title,
  vdm.genre,
  vdm.category,
  vdm.duration_min,
  vdm.attribute_1,
  vdm.attribute_2,
  vdm.attribute_3,
  vdm.attribute_4,
  vdm.attribute_5,
  vdm.attribute_6,
  vdm.attribute_7,
  vdm.attribute_8,
  vdm.attribute_9,
  vdm.attribute_10,
  vdm.attribute_11,
  vdm.attribute_12,
  vdm.attribute_13,
  vdm.attribute_14,
  vdm.attribute_15,
  vdm.attribute_16,
  vdm.attribute_17,
  vdm.attribute_18,
  vdm.attribute_19,
  vdm.attribute_20,
  b.views_subsequent_details_30D
FROM var_vod_master vdm 
LEFT OUTER JOIN (
  SELECT 
    t2.content_id         AS contentid, 
    nvl(sum(t3.loads), 0) AS views_subsequent_details_30D
  FROM var_vod_master t2
  LEFT OUTER JOIN (
          SELECT 
            temp1.contentid,
            IF(temp1.timestamp < temp2.timestamp, 1, 0) AS loads    
          FROM (
               SELECT t1.sessionid, t1.userid, t1.deviceid, t1.contentid, t1.timestamp
                 FROM target_user_action t1
                 WHERE t1.dt_reference >= DATE_SUB(FROM_UNIXTIME(UNIX_TIMESTAMP(),'yyyy-MM-dd') , 30) 
                    AND t1.dt_reference < FROM_UNIXTIME(UNIX_TIMESTAMP(),'yyyy-MM-dd') 
                  AND t1.eventtype = "LOADCONTENTDETAILSPAGE"
               ) temp1 
          JOIN ( 
               SELECT t2.sessionid, t2.userid, t2.deviceid, t2.contentid, t2.timestamp
                 FROM target_user_action t2
                WHERE t2.dt_reference >= DATE_SUB(FROM_UNIXTIME(UNIX_TIMESTAMP(),'yyyy-MM-dd') , 30) 
                    AND t2.dt_reference < FROM_UNIXTIME(UNIX_TIMESTAMP(),'yyyy-MM-dd') 
                  AND t2.eventtype = "VIDEOSTART"
               )temp2 
            ON temp1.userid=temp2.userid
           AND temp1.sessionid=temp2.sessionid
           AND temp1.deviceid=temp2.deviceid
           AND temp1.contentid=temp2.contentid          
    ) t3 ON ( t2.content_id = t3.contentid ) 
 GROUP BY t2.content_id
) b ON ( vdm.content_id = b.contentid );

-- creating a temporary table to store the 3^ subset of VAR Vod's KPIs
CREATE EXTERNAL TABLE IF NOT EXISTS var_vod_30D_tmp03 (
  content_id STRING,
  title STRING,
  genre STRING,
  category STRING,
  duration_min DOUBLE,
  attribute_1 STRING,
  attribute_2 STRING,
  attribute_3 STRING,
  attribute_4 STRING,
  attribute_5 STRING,
  attribute_6 STRING,
  attribute_7 STRING,
  attribute_8 STRING,
  attribute_9 STRING,
  attribute_10 STRING,
  attribute_11 STRING,
  attribute_12 STRING,
  attribute_13 STRING,
  attribute_14 STRING,
  attribute_15 STRING,
  attribute_16 STRING,
  attribute_17 STRING,
  attribute_18 STRING,
  attribute_19 STRING,
  attribute_20 STRING,
  purchases_30D INT,
  revenues_30D DOUBLE
)
ROW FORMAT
DELIMITED FIELDS TERMINATED BY '\073'
LINES TERMINATED BY '\n'
STORED AS TEXTFILE
LOCATION '${hivevar:root_path}/agg/VAR_VOD/var_vod_30D_tmp03';

-- -----------------------------------------------------------------------------
-- Temporary table name: var_vod_30D_tmp03
-- PURCHASES_30D
-- REVENUES_30D
-- -----------------------------------------------------------------------------

INSERT OVERWRITE TABLE var_vod_30D_tmp03
SELECT 
  vdm.content_id,
  vdm.title,
  vdm.genre,
  vdm.category,
  vdm.duration_min,
  vdm.attribute_1,
  vdm.attribute_2,
  vdm.attribute_3,
  vdm.attribute_4,
  vdm.attribute_5,
  vdm.attribute_6,
  vdm.attribute_7,
  vdm.attribute_8,
  vdm.attribute_9,
  vdm.attribute_10,
  vdm.attribute_11,
  vdm.attribute_12,
  vdm.attribute_13,
  vdm.attribute_14,
  vdm.attribute_15,
  vdm.attribute_16,
  vdm.attribute_17,
  vdm.attribute_18,
  vdm.attribute_19,
  vdm.attribute_20,
  b.purchases_30D,
  b.revenues_30D
FROM var_vod_master vdm 
LEFT OUTER JOIN (
  SELECT 
    t2.content_id                 AS contentid,
    count(t3.userid)              AS purchases_30D,
    nvl(sum(t3.originalprice), 0) AS revenues_30D
  FROM var_vod_master t2
  LEFT OUTER JOIN (
    SELECT 
      t0.contentid     AS contentid,
      t0.originalprice AS originalprice,
      t0.userid        AS userid
    FROM target_purchase t0 
    JOIN target_category_map t1 ON (t0.contentid = t1.contentid)
  WHERE t0.dt_reference >= DATE_SUB(FROM_UNIXTIME(UNIX_TIMESTAMP(),'yyyy-MM-dd') , 30) 
                    AND t0.dt_reference < FROM_UNIXTIME(UNIX_TIMESTAMP(),'yyyy-MM-dd') 
    ) t3 ON ( t2.content_id = t3.contentid ) 
 GROUP BY 
   t2.content_id
) b ON ( vdm.content_id = b.contentid );

-- creating a temporary table to store the 4^ subset of VAR Vod's KPIs
CREATE EXTERNAL TABLE IF NOT EXISTS var_vod_30D_tmp04 (
  content_id STRING,
  title STRING,
  genre STRING,
  category STRING,
  duration_min DOUBLE,
  attribute_1 STRING,
  attribute_2 STRING,
  attribute_3 STRING,
  attribute_4 STRING,
  attribute_5 STRING,
  attribute_6 STRING,
  attribute_7 STRING,
  attribute_8 STRING,
  attribute_9 STRING,
  attribute_10 STRING,
  attribute_11 STRING,
  attribute_12 STRING,
  attribute_13 STRING,
  attribute_14 STRING,
  attribute_15 STRING,
  attribute_16 STRING,
  attribute_17 STRING,
  attribute_18 STRING,
  attribute_19 STRING,
  attribute_20 STRING,
  downloads_30D INT
)
ROW FORMAT
DELIMITED FIELDS TERMINATED BY '\073'
LINES TERMINATED BY '\n'
STORED AS TEXTFILE
LOCATION '${hivevar:root_path}/agg/VAR_VOD/var_vod_30D_tmp04';

-- -----------------------------------------------------------------------------
-- Temporary table name: var_vod_30D_tmp04
-- DOWNLOADS_30D
-- -----------------------------------------------------------------------------

INSERT OVERWRITE TABLE var_vod_30D_tmp04
SELECT 
  vdm.content_id,
  vdm.title,
  vdm.genre,
  vdm.category,
  vdm.duration_min,
  vdm.attribute_1,
  vdm.attribute_2,
  vdm.attribute_3,
  vdm.attribute_4,
  vdm.attribute_5,
  vdm.attribute_6,
  vdm.attribute_7,
  vdm.attribute_8,
  vdm.attribute_9,
  vdm.attribute_10,
  vdm.attribute_11,
  vdm.attribute_12,
  vdm.attribute_13,
  vdm.attribute_14,
  vdm.attribute_15,
  vdm.attribute_16,
  vdm.attribute_17,
  vdm.attribute_18,
  vdm.attribute_19,
  vdm.attribute_20,
  b.downloads_30D
FROM var_vod_master vdm 
LEFT OUTER JOIN (
  SELECT 
    t2.content_id    AS content_id,
    sum(t3.download) AS downloads_30D
  FROM var_vod_master t2
  LEFT OUTER JOIN (
    SELECT 
      t0.contentid                 AS contentid,
      IF (t0.download = "Y", 1, 0) AS download
    FROM target_watching t0 
    LEFT OUTER JOIN target_category_map t1 ON (t0.contentid = t1.contentid)
   WHERE t0.dt_reference >= DATE_SUB(FROM_UNIXTIME(UNIX_TIMESTAMP(),'yyyy-MM-dd') , 30) 
                    AND t0.dt_reference < FROM_UNIXTIME(UNIX_TIMESTAMP(),'yyyy-MM-dd') 
    ) t3 ON ( t2.content_id = t3.contentid ) 
 GROUP BY 
   t2.content_id
) b ON ( vdm.content_id = b.content_id );

-- creating a temporary table to store the 5^ subset of VAR Vod's KPIs
CREATE EXTERNAL TABLE IF NOT EXISTS var_vod_30D_tmp05 (
  content_id STRING,
  title STRING,
  genre STRING,
  category STRING,
  duration_min DOUBLE,
  attribute_1 STRING,
  attribute_2 STRING,
  attribute_3 STRING,
  attribute_4 STRING,
  attribute_5 STRING,
  attribute_6 STRING,
  attribute_7 STRING,
  attribute_8 STRING,
  attribute_9 STRING,
  attribute_10 STRING,
  attribute_11 STRING,
  attribute_12 STRING,
  attribute_13 STRING,
  attribute_14 STRING,
  attribute_15 STRING,
  attribute_16 STRING,
  attribute_17 STRING,
  attribute_18 STRING,
  attribute_19 STRING,
  attribute_20 STRING,
  playlists_30D INT
)
ROW FORMAT
DELIMITED FIELDS TERMINATED BY '\073'
LINES TERMINATED BY '\n'
STORED AS TEXTFILE
LOCATION '${hivevar:root_path}/agg/VAR_VOD/var_vod_30D_tmp05';

-- -----------------------------------------------------------------------------
-- Temporary table name: var_vod_30D_tmp05
-- PLAYLISTS_30D
-- -----------------------------------------------------------------------------

INSERT OVERWRITE TABLE var_vod_30D_tmp05
SELECT 
  vdm.content_id,
  vdm.title,
  vdm.genre,
  vdm.category,
  vdm.duration_min,
  vdm.attribute_1,
  vdm.attribute_2,
  vdm.attribute_3,
  vdm.attribute_4,
  vdm.attribute_5,
  vdm.attribute_6,
  vdm.attribute_7,
  vdm.attribute_8,
  vdm.attribute_9,
  vdm.attribute_10,
  vdm.attribute_11,
  vdm.attribute_12,
  vdm.attribute_13,
  vdm.attribute_14,
  vdm.attribute_15,
  vdm.attribute_16,
  vdm.attribute_17,
  vdm.attribute_18,
  vdm.attribute_19,
  vdm.attribute_20,
  b.playlist_30D
FROM var_vod_master vdm 
LEFT OUTER JOIN (
  SELECT 
    t2.content_id    AS contentid, 
    count(t3.userid) AS playlist_30D
  FROM var_vod_master t2
  LEFT OUTER JOIN (
    SELECT 
      t0.contentid AS contentid,
      t0.user_id   AS userid
    FROM target_favourite t0 
    JOIN target_category_map t1 ON (t0.contentid = t1.contentid)
   WHERE t0.dt_reference >= DATE_SUB(FROM_UNIXTIME(UNIX_TIMESTAMP(),'yyyy-MM-dd') , 30) 
                    AND t0.dt_reference < FROM_UNIXTIME(UNIX_TIMESTAMP(),'yyyy-MM-dd') 
    ) t3 ON ( t2.content_id = t3.contentid ) 
 GROUP BY 
   t2.content_id
) b ON ( vdm.content_id = b.contentid );

-- creating a temporary table to store the 6^ subset of VAR Vod's KPIs
CREATE EXTERNAL TABLE IF NOT EXISTS var_vod_30D_tmp06 (
  content_id STRING,
  title STRING,
  genre STRING,
  category STRING,
  duration_min DOUBLE,
  attribute_1 STRING,
  attribute_2 STRING,
  attribute_3 STRING,
  attribute_4 STRING,
  attribute_5 STRING,
  attribute_6 STRING,
  attribute_7 STRING,
  attribute_8 STRING,
  attribute_9 STRING,
  attribute_10 STRING,
  attribute_11 STRING,
  attribute_12 STRING,
  attribute_13 STRING,
  attribute_14 STRING,
  attribute_15 STRING,
  attribute_16 STRING,
  attribute_17 STRING,
  attribute_18 STRING,
  attribute_19 STRING,
  attribute_20 STRING,
  impressions_30D INT
)
ROW FORMAT
DELIMITED FIELDS TERMINATED BY '\073'
LINES TERMINATED BY '\n'
STORED AS TEXTFILE
LOCATION '${hivevar:root_path}/agg/VAR_VOD/var_vod_30D_tmp06';

-- -----------------------------------------------------------------------------
-- Temporary table name: var_vod_30D_tmp06
-- IMPRESSIONS_30D
-- -----------------------------------------------------------------------------

INSERT OVERWRITE TABLE var_vod_30D_tmp06
SELECT 
  vdm.content_id,
  vdm.title,
  vdm.genre,
  vdm.category,
  vdm.duration_min,
  vdm.attribute_1,
  vdm.attribute_2,
  vdm.attribute_3,
  vdm.attribute_4,
  vdm.attribute_5,
  vdm.attribute_6,
  vdm.attribute_7,
  vdm.attribute_8,
  vdm.attribute_9,
  vdm.attribute_10,
  vdm.attribute_11,
  vdm.attribute_12,
  vdm.attribute_13,
  vdm.attribute_14,
  vdm.attribute_15,
  vdm.attribute_16,
  vdm.attribute_17,
  vdm.attribute_18,
  vdm.attribute_19,
  vdm.attribute_20,
  b.impressions_30D
FROM var_vod_master vdm 
LEFT OUTER JOIN (
  SELECT 
    t2.content_id    AS contentid, 
    count(t3.userid) AS impressions_30D
  FROM var_vod_master t2
  LEFT OUTER JOIN (
    SELECT 
      t0.content_id AS contentid,
      t0.user_id    AS userid
    FROM target_impression t0 
    JOIN target_category_map t1 ON (t0.content_id = t1.contentid)
  WHERE t0.dt_reference >= DATE_SUB(FROM_UNIXTIME(UNIX_TIMESTAMP(),'yyyy-MM-dd') , 30) 
                    AND t0.dt_reference < FROM_UNIXTIME(UNIX_TIMESTAMP(),'yyyy-MM-dd') 
    ) t3 ON ( t2.content_id = t3.contentid ) 
 GROUP BY 
   t2.content_id
) b ON ( vdm.content_id = b.contentid );

-- creating a temporary table to store the 7^ subset of VAR Vod's KPIs
CREATE EXTERNAL TABLE IF NOT EXISTS var_vod_30D_tmp07 (
  content_id STRING,
  title STRING,
  genre STRING,
  category STRING,
  duration_min DOUBLE,
  attribute_1 STRING,
  attribute_2 STRING,
  attribute_3 STRING,
  attribute_4 STRING,
  attribute_5 STRING,
  attribute_6 STRING,
  attribute_7 STRING,
  attribute_8 STRING,
  attribute_9 STRING,
  attribute_10 STRING,
  attribute_11 STRING,
  attribute_12 STRING,
  attribute_13 STRING,
  attribute_14 STRING,
  attribute_15 STRING,
  attribute_16 STRING,
  attribute_17 STRING,
  attribute_18 STRING,
  attribute_19 STRING,
  attribute_20 STRING,
  likes_30D INT,
  dislikes_30D INT,
  ratings_30D INT,
  avg_rating_value_30D DOUBLE
)
ROW FORMAT
DELIMITED FIELDS TERMINATED BY '\073'
LINES TERMINATED BY '\n'
STORED AS TEXTFILE
LOCATION '${hivevar:root_path}/agg/VAR_VOD/var_vod_30D_tmp07';

-- -----------------------------------------------------------------------------
-- Temporary table name: var_vod_30D_tmp07
-- LIKES_30D
-- DISLIKES_30D
-- RATINGS_30D
-- AVG_RATING_VALUE_30D
-- -----------------------------------------------------------------------------

INSERT OVERWRITE TABLE var_vod_30D_tmp07
SELECT 
  vdm.content_id,
  vdm.title,
  vdm.genre,
  vdm.category,
  vdm.duration_min,
  vdm.attribute_1,
  vdm.attribute_2,
  vdm.attribute_3,
  vdm.attribute_4,
  vdm.attribute_5,
  vdm.attribute_6,
  vdm.attribute_7,
  vdm.attribute_8,
  vdm.attribute_9,
  vdm.attribute_10,
  vdm.attribute_11,
  vdm.attribute_12,
  vdm.attribute_13,
  vdm.attribute_14,
  vdm.attribute_15,
  vdm.attribute_16,
  vdm.attribute_17,
  vdm.attribute_18,
  vdm.attribute_19,
  vdm.attribute_20,
  b.likes_30D,
  b.dislikes_30D,
  b.ratings_30D,
  b.avg_rating_value_30D
FROM var_vod_master vdm 
LEFT OUTER JOIN (
  SELECT 
    t2.content_id, 
    nvl(sum(t3.is_liked), 0)                         AS likes_30D,
    nvl(sum(t3.is_disliked), 0)                      AS dislikes_30D,
    nvl(sum(t3.rating), 0)                           AS ratings_30D,
    round(sum(t3.avg_rating)/count(t2.content_id),2) AS avg_rating_value_30D
  FROM var_vod_master t2
  LEFT OUTER JOIN (
    SELECT 
      t0.content_id                                 AS contentid,
      IF (t0.is_liked = "Y", 1, 0)                  AS is_liked,
      IF (t0.is_disliked = "Y", 1,0)                AS is_disliked,
      IF (t0.rating in ("1","2","3","4","5"), 0, 1) AS rating,
      t0.rating                                     AS avg_rating
    FROM target_curation t0 
    LEFT OUTER JOIN target_category_map t1 ON (t0.content_id = t1.contentid)
   WHERE t0.dt_reference >= DATE_SUB(FROM_UNIXTIME(UNIX_TIMESTAMP(),'yyyy-MM-dd') , 30) 
                    AND t0.dt_reference < FROM_UNIXTIME(UNIX_TIMESTAMP(),'yyyy-MM-dd') 
    ) t3 ON ( t2.content_id = t3.contentid ) 
 GROUP BY 
   t2.content_id
) b ON ( vdm.content_id = b.content_id );

-- -----------------------------------------------------------------------------
-- Temporary table name: var_vod_30D
-- JOIN ALL TEMPORARY TABLES
-- -----------------------------------------------------------------------------

CREATE EXTERNAL TABLE IF NOT EXISTS var_vod_30D (
  content_id STRING,
  title STRING,
  genre STRING,
  category STRING,
  duration_min STRING,
  attribute_1 STRING,
  attribute_2 STRING,
  attribute_3 STRING,
  attribute_4 STRING,
  attribute_5 STRING,
  attribute_6 STRING,
  attribute_7 STRING,
  attribute_8 STRING,
  attribute_9 STRING,
  attribute_10 STRING,
  attribute_11 STRING,
  attribute_12 STRING,
  attribute_13 STRING,
  attribute_14 STRING,
  attribute_15 STRING,
  attribute_16 STRING,
  attribute_17 STRING,
  attribute_18 STRING,
  attribute_19 STRING,
  attribute_20 STRING,
  logged_viewers_30D INT,
  anonymous_viewers_30D INT,
  logged_views_30D INT,
  anonymous_views_30D INT,
  playback_duration_min_per_logged_viewer_30D DOUBLE,
  playback_duration_min_per_anonymous_viewer_30D DOUBLE,
  playback_duration_min_per_logged_view_30D DOUBLE,
  playback_duration_min_per_anonymous_view_30D DOUBLE,
  views_subsequent_details_30D INT,
  purchases_30D INT,
  revenues_30D double,
  downloads_30D INT,
  playlists_30D INT,
  impressions_30D INT,
  likes_30D INT,
  dislikes_30D INT,
  ratings_30D INT,
  avg_rating_value_30D INT
)
ROW FORMAT
DELIMITED FIELDS TERMINATED BY '\073'
LINES TERMINATED BY '\n'
STORED AS TEXTFILE
LOCATION '${hivevar:root_path}/agg/VAR_VOD/var_vod_30D';

INSERT OVERWRITE TABLE var_vod_30D
SELECT t1.*, 
       t2.views_subsequent_details_30D, 
       t3.purchases_30D,
       t3.revenues_30D,
       t4.downloads_30D,
       t5.playlists_30D,
       t6.impressions_30D,
       t7.likes_30D,
       t7.dislikes_30D,
       t7.ratings_30D,
       t7.avg_rating_value_30D
FROM var_vod_30D_tmp01 t1
JOIN var_vod_30D_tmp02 t2 ON t1.content_id = t2.content_id
JOIN var_vod_30D_tmp03 t3 ON t1.content_id = t3.content_id
JOIN var_vod_30D_tmp04 t4 ON t1.content_id = t4.content_id
JOIN var_vod_30D_tmp05 t5 ON t1.content_id = t5.content_id
JOIN var_vod_30D_tmp06 t6 ON t1.content_id = t6.content_id
JOIN var_vod_30D_tmp07 t7 ON t1.content_id = t7.content_id;
